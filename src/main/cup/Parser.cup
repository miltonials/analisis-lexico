package com.compi;

import java_cup.runtime.*;
import java.io.*;
import java.util.*;
import java.lang.Math;

//para errores
import java_cup.runtime.Symbol;

//para tablas de simbolos
import java.util.HashMap;
import java.util.Map;
import java.util.ArrayList;

/**
 * Clase principal del compilador
 */
class Parser;


action code {:
        HashMap<String, ArrayList<String>> listaTablasSimbolos = new HashMap<String, ArrayList<String>>();
        String currentHash;
        boolean compila = true;
        String vName;
        String vValue;
        char vValueCharmander;

        /**
         * Imprime la tabla de símbolos
        */
        public void imprimirTablaSimbolos () {
            System.out.println("_________________________Tablas de símbolos_________________________");
            for(String key: listaTablasSimbolos.keySet()) {
                System.out.println("Tabla de símbolos: " + key);
                System.out.println("valores: ");
                for (String item : listaTablasSimbolos.get(key)) {
                    System.out.println(item);
                }
                System.out.println("");
            }
            System.out.println("______________________________________________________________________");
        }

        /**
         * Agrega una tabla de símbolos a la lista de tablas de símbolos
         * @param tipo tipo de la tabla de símbolos
         * @param nombreFuncion nombre de la función a la que pertenece la tabla de símbolos
         */
        public void agregarTablaSimbolos (String tipo, String nombreFuncion) {
            String tipoTabla = "Tipo: " + tipo;
            ArrayList<String> funcionMain = new ArrayList<String>();
            currentHash = nombreFuncion;
            funcionMain.add(tipoTabla);
            listaTablasSimbolos.put(currentHash, funcionMain);
        }

        /*
         * muestra el error de sintaxis
         * @param mensaje error
         */
        public void error(String mensaje) {
            System.out.println(mensaje);
        }
:}

parser code {:
        IdLexer miParser;
        // Habilitar la depuración
        boolean _cup_debug = true;
        List<String> syntaxErrors = new ArrayList<>();

        String tipoRetornoActual = null;

        /**
         * Establece el tipo de retorno actual
         * @param tipo tipo de retorno actual
         */
        public void setTipoRetorno(String tipo) {
                tipoRetornoActual = tipo;
        }

        /**
         * Verifica que el tipo de retorno sea el correcto
         * @param tipoReal tipo de retorno real
         */
        public void checkTipoRetorno(String tipoReal) {
                if (tipoRetornoActual != null && !tipoRetornoActual.equals(tipoReal)) {
                        System.err.println("Tipo de retorno incorrecto. Se esperaba: " + tipoRetornoActual + ", pero se encontró: " + tipoReal);
                }
        }

        // Agregar un método para obtener la lista de errores después del análisis
        /**
         * Obtiene la lista de errores de sintaxis
         * @return lista de errores de sintaxis
         */
        public List<String> getSyntaxErrors() {
                return syntaxErrors;
        }

        @SuppressWarnings("deprecation")
        public Parser(IdLexer miParser) {
                this.miParser = miParser;
                this.symbolFactory = new DefaultSymbolFactory();
        }

        /**
         * captura los errores de sintaxis 
         * @param s error
         */
        public void syntax_error(Symbol s) {
                try {
                        String errorMessage = "Error de sintaxis: " + s.value + " en la línea " + (s.left + 1) + " columna " + (s.right + 1);
                        System.err.println(errorMessage);
                        // Agregar lógica adicional si es necesario para errores recuperables
                        //throw new Exception("Error recuperable");
                } catch (Exception e) {
                        System.err.println("Hola");
                        System.err.println("Error de sintaxis: " + e.getMessage());
                }
        }

        /**
         * captura los errores de sintaxis no recuperables
         * @param s error
         */
        public void unrecovered_syntax_error(Symbol s) throws java.lang.Exception {
                String errorMessage = "Error de sintaxis no recuperable: " + s.value + " en la línea " + (s.left + 1) + " columna " + (s.right + 1);
                System.err.println(errorMessage);

                // Agregar lógica adicional si es necesario para errores no recuperables
                // throw new Exception("Error no recuperable");
        }
:}


init with {: :};

scan with {: return miParser.next_token(); :};

/* Terminales */

// Operadores aritméticos binarios
terminal String RUDOLPH; // +
terminal String DASHER; // -
terminal String  DANCER; // /
terminal String  PRANCER; // // (division entera)
terminal String  VIXEN; // *
terminal String  COMET; // ~
terminal String CUPID; // **
// Operadores aritméticos unarios
terminal String  GRINCH; // ++
terminal String  QUIEN; // --
// Operadores relacionales
terminal String  HERMEY; // <
terminal String  BUDDY; // =<
terminal String  JINGLE; // >
terminal String JANGLE; // >=
terminal String  PEPPERMINT; // ==
terminal String  WUNORSE; // !=
// Operadores lógicos
terminal String  MELCHOR; // ^
terminal String  GASPAR; // #
terminal String BALTASAR; // !
terminal  LOCAL; // variable

// Tipos
terminal String PERSONA; // identificador
terminal String SANTACLAUS; // int
terminal String PERENOEL; // float
terminal String FATHERCHRISTMAS; // boolean
terminal String KRISKRINGLE; // char
terminal String DEDMOROZ; // string
terminal Integer[] PAPANOEL; // array
terminal Integer L_SANTACLAUS; // literal int
terminal Double L_PERENOEL; // literal float
//terminal Boolean L_FATHERCHRISTMAS; // literal boolean
terminal String L_FATHERCHRISTMAS; // literal boolean
terminal String L_KRISKRINGLE; // literal char
terminal String L_DEDMOROZ; // literal string
//terminal Integer[] L_PAPANOEL; // literal array

// Parentesis
terminal String ABRECUENTO; // (
terminal String CIERRECUENTO; // )
terminal String ABREEMPAQUE; // [
terminal String CIERREEMPAQUE; // ]
terminal String ABREREGALO; // {
terminal String CIERRAREGALO; // }

// Estructuras de control
terminal String ELFO;//if
terminal String HADA;//else
terminal String DUENDE;//else
terminal String ENVUELVE;//for
terminal String HACE;//do
terminal String REVISA;//until
terminal String ENVIA;//return
terminal String CORTA;//break

// Entrada y salida
terminal Integer NARRA;//print
terminal Integer ESCUCHA;//read 

// Fin de expresión
terminal  FINREGALO; // |

// Lexema separador
terminal String COMA; //,

// Asignación
terminal Integer ENTREGA;//<=

// Comentarios
// son ignorados por el compilador

// Funciones
terminal Integer MAIN; //main
terminal Integer REGALO; // definicion de funcion 

//-------------------------------------------------------------------------------------- NO Terminales
non terminal navidad; // Símbolo inicial ✅
non terminal bolsanavidena; // produccion para funciones ✅
non terminal regaloprincipal; // produccion para main ✅
non terminal regalofuncional; // produccion para funciones ✅
non terminal regalofuncionaltipado; // produccion para funciones ✅
non terminal juguetes; // produccion para expresiones ✅
non terminal tipojuguetes; // produccion para expresiones ✅
non terminal regalofuncionalentero; // produccion para funciones int
non terminal regalofuncionalflotante; // produccion para funciones float
non terminal regalofuncionaldelaverdad; // produccion para funciones boolean
non terminal regalofuncionalcharmander; // produccion para funciones char
non terminal encabezadoregalofuncional; // produccion para "nombreFuncion params {"
non terminal dedicatoriasenteras; // produccion para " return int| } "
non terminal dedicatoriasflotantes; // produccion para  " return float| } "
non terminal dedicatoriasdelaverdad; // produccion para " return boolean| } "
non terminal dedicatoriascharmander; // produccion para  " return char| } "
non terminal monnos; // produccion para parametros de funcion ✅
non terminal tiposmonnos; // produccion para parametros de funcion ✅
non terminal juguetesaritmeticos; // produccion para expresiones aritméticas ✅
non terminal juguetesrelacionales; // produccion para expresiones relacionales
non terminal piezasrelacionales; // produccion para expresiones relacionales ✅
non terminal jugueteslogicos; // produccion para expresiones lógicas ✅
non terminal juguetesunarios; // produccion para expresiones unarias ✅
non terminal jugueteasignacion; // produccion para expresiones de asignacion y ciclos✅
non terminal arreglosarreglados; // produccion para arreglos ✅
non terminal juguetesdecontrol; // produccion para if-elif-else ✅
non terminal juguetesaritmeticos_operadores; // produccion para operadores aritméticos ✅
non terminal juguetesdefinidos; // produccion para definicion de variables ✅
non terminal controldejuguete; // produccion para condicionales de estructura ✅
non terminal adicionalesdecontrol; // produccion para los if anidados y el else ✅
non terminal iteradordejuguetes; // produccion para ciclos ✅      
non terminal parametrosdeenvoltura; // produccion para del ciclo ✅
non terminal jugueteparlanchin; // produccion para print ✅
non terminal juguetechismoso; // produccion para read ✅
non terminal etiquetanavidena; // produccion para sentencias ✅ 
non terminal piezasenteras; // produccion para operaciones literales enteras ✅ 
non terminal piezasflotantes; // produccion para operaciones literales flotantes ✅
non terminal jugueteasignacion_entera; // produccion para asignacion de variables enteros ✅
non terminal jugueteasignacion_flotante; // produccion para asignacion de variables flotantes ✅
non terminal piezaarregloentero; // produccion para arreglos enteros ✅
non terminal piezaarreglochar; // produccion para arreglos char ✅
non terminal error_production; // produccion para errores ✅
//-------------------------------------------------------------------------------------- PRECEDENCIA
precedence left RUDOLPH, DASHER; // suma y resta
precedence left DANCER, PRANCER; // division y division entera
precedence left VIXEN, COMET; // multiplicacion y modulo
precedence left CUPID; // potencia
precedence left GRINCH, QUIEN; // incremento y decremento
precedence left HERMEY, BUDDY, JINGLE, JANGLE, PEPPERMINT, WUNORSE; // operadores relacionales
precedence left MELCHOR, GASPAR; // operadores lógicos
precedence left BALTASAR; // operador lógico not
precedence left COMA; // lexema separador





//-------------------------------------------------------------------------------------- PRODUCCIONES
/* Símbolo inicial y producciones gramaticales*/
start with navidad;

/* Producciones gramaticales */
navidad ::= 
        bolsanavidena
        {:
            imprimirTablaSimbolos();
            System.out.println("Fin de ejecución");
            
            if (compila) {
                System.out.println("Compilación exitosa");
            } else {
                System.out.println("Compilación fallida");
            }
        :}
        ;

bolsanavidena ::=  regaloprincipal
                | regalofuncional regaloprincipal;

regaloprincipal ::= REGALO SANTACLAUS MAIN ABRECUENTO  CIERRECUENTO ABREREGALO
                    {:
                        agregarTablaSimbolos("int", "main");
                    :}
                    juguetes
                    dedicatoriasenteras
                    | error_production; //{: System.out.println("Error en la expresión de regaloprincipal"); :};

//Funciones
regalofuncional ::= regalofuncionaltipado | regalofuncional regalofuncionaltipado;

regalofuncionaltipado ::= regalofuncionalentero | regalofuncionalflotante | regalofuncionaldelaverdad
                | regalofuncionalcharmander
                | error_production; //{: System.out.println("Error en la expresión de regalofuncionaltipado"); :};


regalofuncionalentero ::= REGALO SANTACLAUS:tFunc PERSONA:funName
                        {:
                            agregarTablaSimbolos(tFunc.toString(), funName.toString());setTipoRetorno("int");
                        :}
                            encabezadoregalofuncional juguetes dedicatoriasenteras;

regalofuncionalflotante ::= REGALO PERENOEL:tFunc PERSONA:funName
                            {:
                                agregarTablaSimbolos(tFunc.toString(), funName.toString());
                            :}
                            encabezadoregalofuncional juguetes dedicatoriasflotantes;

regalofuncionaldelaverdad ::= REGALO FATHERCHRISTMAS:tFunc PERSONA:funName
                            {:
                                agregarTablaSimbolos(tFunc.toString(), funName.toString());
                            :}
                            encabezadoregalofuncional juguetes dedicatoriasdelaverdad;

regalofuncionalcharmander ::= REGALO KRISKRINGLE:tFunc PERSONA:funName
                            {:
                                agregarTablaSimbolos(tFunc.toString(), funName.toString());
                            :}
                            encabezadoregalofuncional juguetes dedicatoriascharmander;


// dedicatoriasenteras ::= ENVIA piezasenteras FINREGALO CIERRAREGALO; // | ENVIA PERSONA FINREGALO CIERRAREGALO;
dedicatoriasenteras ::= ENVIA piezasenteras FINREGALO CIERRAREGALO {: checkTipoRetorno("int"); :};
                //      | ENVIA PERSONA FINREGALO CIERRAREGALO;

dedicatoriasflotantes ::= ENVIA piezasflotantes FINREGALO CIERRAREGALO; // | ENVIA PERSONA FINREGALO CIERRAREGALO;
dedicatoriasdelaverdad ::= ENVIA jugueteslogicos FINREGALO CIERRAREGALO | ENVIA PERSONA FINREGALO CIERRAREGALO;
dedicatoriascharmander ::= ENVIA L_KRISKRINGLE FINREGALO CIERRAREGALO | ENVIA PERSONA FINREGALO CIERRAREGALO;

//para parametros
encabezadoregalofuncional ::= ABRECUENTO CIERRECUENTO ABREREGALO
                            | ABRECUENTO monnos CIERRECUENTO ABREREGALO
                ; //{: System.out.println("Error en la expresión de encabezadoregalofuncional"); :};


// Parámetros de la función
monnos ::= tiposmonnos | tiposmonnos COMA monnos;

// tiposmonnos son los diferentes tipos de parámetros de la función
tiposmonnos ::= SANTACLAUS PERSONA:idParam // int identificador
                {:
                //int
                    listaTablasSimbolos.get(currentHash).add("Parámetro int : " + idParam);
                :}   
            | PERENOEL PERSONA:idParam // float identificador
                {:
                //float
                    listaTablasSimbolos.get(currentHash).add("Parámetro float : " + idParam);
                :}   
            | FATHERCHRISTMAS PERSONA:idParam // boolean identificador
                {:
                //boolean
                    listaTablasSimbolos.get(currentHash).add("Parámetro boolean : " + idParam);
                :}   
            | KRISKRINGLE PERSONA:idParam // char identificador
                {:
                //char
                    listaTablasSimbolos.get(currentHash).add("Parámetro char : " + idParam);
                :}   
            | DEDMOROZ PERSONA:idParam // string identificador
                {:
                //string
                    listaTablasSimbolos.get(currentHash).add("Parámetro string : " + idParam);
                :}   
            | PAPANOEL PERSONA:idParam // array identificador
                {:
                //array
                    listaTablasSimbolos.get(currentHash).add("Parámetro array : " + idParam);
                :}   
            | error_production; //{: System.out.println("Error en la expresión de tiposmonnos"); :};

juguetes ::= etiquetanavidena
        | etiquetanavidena CORTA FINREGALO
        | etiquetanavidena CORTA FINREGALO etiquetanavidena
        | error_production; //{: System.out.println("Error en la expresión de juguetes"); };


etiquetanavidena ::= tipojuguetes | tipojuguetes etiquetanavidena;

tipojuguetes ::= juguetesaritmeticos FINREGALO // aritméticas (sumas, restas, etc)✅
         | juguetesrelacionales FINREGALO // expresiones relacionales (menor, mayor, etc) 
         | jugueteslogicos FINREGALO// expresiones lógicas (and, or, etc)
         | juguetesdefinidos FINREGALO // expresiones de definicion de variables
         | jugueteasignacion FINREGALO // expresiones de asignacion y ciclos
         | juguetesdecontrol
         | iteradordejuguetes
         | jugueteparlanchin FINREGALO
         | juguetechismoso FINREGALO;

jugueteparlanchin ::= NARRA ABRECUENTO PERSONA CIERRECUENTO | NARRA ABRECUENTO L_DEDMOROZ CIERRECUENTO;//TODO: concatenación de strings




juguetechismoso ::= ESCUCHA ABRECUENTO CIERRECUENTO //read ()
                | ESCUCHA ABRECUENTO PERSONA CIERRECUENTO //read (str)
                //| LOCAL DEDMOROZ PERSONA ENTREGA ESCUCHA ABRECUENTO L_DEDMOROZ CIERRECUENTO // LOCAL DEDMOROZ str ENTREGA ESCUCHA ("str")
                //| LOCAL DEDMOROZ PERSONA ENTREGA ESCUCHA ABRECUENTO CIERRECUENTO  // LOCAL DEDMOROZ str ENTREGA ESCUCHA ()
                | error_production; //{: System.out.println("Error en la expresión de juguetechismoso"); :};

// aritméticas (sumas, restas, etc)
juguetesaritmeticos ::= piezasenteras | piezasflotantes;

/*
juguetesaritmeticos ::= juguetesaritmeticos:e1 RUDOLPH juguetesaritmeticos:e2
                        {:
                            System.out.print("-------------" + e1 + "\t");
                            System.out.println(e2);
                          :} // +
                     |  juguetesaritmeticos DASHER juguetesaritmeticos // -
                     |  juguetesaritmeticos DANCER juguetesaritmeticos // /
                     |  juguetesaritmeticos PRANCER juguetesaritmeticos // //
                     |  juguetesaritmeticos VIXEN juguetesaritmeticos // *
                     |  juguetesaritmeticos COMET juguetesaritmeticos // ~
                     |  juguetesaritmeticos CUPID juguetesaritmeticos // **
                     |  ABRECUENTO juguetesaritmeticos CIERRECUENTO // (expr)
                     |  ABRECUENTO juguetesaritmeticos CIERRECUENTO juguetesaritmeticos_operadores juguetesaritmeticos // (expr) + expr
                     |  juguetesaritmeticos juguetesaritmeticos_operadores ABRECUENTO juguetesaritmeticos CIERRECUENTO // (expr) + expr
                     | juguetesunarios // ++ o --
                     |  PERSONA
                     |  L_PERENOEL
                     |  L_SANTACLAUS:e
                        {:
                            RESULT=e;
                        :}; // +;

*/
juguetesaritmeticos_operadores ::= RUDOLPH:opAritmetico // +
                    {:
                        RESULT = opAritmetico;
                    :}
                |  DASHER:opAritmetico // -
                    {:
                        RESULT = opAritmetico;
                    :}
                |  DANCER:opAritmetico // /
                    {:
                        RESULT = opAritmetico;
                    :}
                |  PRANCER:opAritmetico // //
                    {:
                        RESULT = opAritmetico;
                    :}
                |  VIXEN:opAritmetico // *
                    {:
                        RESULT = opAritmetico;
                    :}
                |  COMET:opAritmetico // ~
                    {:
                        RESULT = opAritmetico;
                    :}
                |  CUPID:opAritmetico // **
                    {:
                        RESULT = opAritmetico;
                    :}
                ;

piezasenteras ::= piezasenteras:valueA juguetesaritmeticos_operadores:opAritmetico piezasenteras:valueB
                    {:
                        RESULT = valueA + " " + opAritmetico.toString() + " " + valueB;

                    :}
                |  ABRECUENTO:lParen piezasenteras:valueA CIERRECUENTO:rParen // (expr)
                    {:
                        RESULT = lParen + " " + valueA + " " + rParen;
                    :}
                |  ABRECUENTO:lParen piezasenteras:valueA CIERRECUENTO:rParen juguetesaritmeticos_operadores:opAritmetico piezasenteras:valueB // (expr) + expr
                    {:
                        RESULT = lParen + " " + valueA + " " + rParen + " " + opAritmetico.toString() + " " + valueB;
                    :}
                |  PERSONA:valueA
                    {:
                        RESULT = valueA;
                    :}
                |  L_SANTACLAUS:valueA
                    {:
                        RESULT = valueA;
                    :}
                ;


piezasflotantes ::= piezasflotantes:valueA juguetesaritmeticos_operadores:opAritmetico piezasflotantes:valueB
                    {:
                        RESULT = valueA + " " + opAritmetico.toString() + " " + valueB;

                    :}
                |  ABRECUENTO:lParen piezasflotantes:valueA CIERRECUENTO:rParen // (expr)
                    {:
                        RESULT = lParen + " " + valueA + " " + rParen;
                    :}
                |  ABRECUENTO:lParen piezasflotantes:valueA CIERRECUENTO:rParen juguetesaritmeticos_operadores:opAritmetico piezasflotantes:valueB // (expr) + expr
                    {:
                        RESULT = lParen + " " + valueA + " " + rParen + " " + opAritmetico.toString() + " " + valueB;
                    :}
                |  PERSONA:valueA VIXEN:op L_PERENOEL:valueB
                    {:
                        RESULT = valueA + " " + op + " " + valueB;
                    :}
                |  L_PERENOEL:valueA
                    {:
                        RESULT = valueA;
                    :}
                ;

// relacionales (menor, mayor, etc)
juguetesrelacionales ::= piezasenteras piezasrelacionales piezasenteras // enteros
        | piezasflotantes piezasrelacionales piezasflotantes; // flotantes

piezasrelacionales ::= HERMEY // <
        | BUDDY // =<
        | JINGLE // >
        | JANGLE // >=
        | PEPPERMINT // ==
        | WUNORSE; // !=

// lógicos (and, or, negacion)
jugueteslogicos ::= jugueteslogicos:valueA MELCHOR jugueteslogicos:valueB // AND
                    {:
                        RESULT = valueA + " ^ " + valueB;
                        vValue = RESULT.toString();
                    :}
        | jugueteslogicos:valueA GASPAR jugueteslogicos:valueB // OR
                    {:
                        RESULT = valueA + " # " + valueB;
                        vValue = RESULT.toString();
                    :}
        | BALTASAR jugueteslogicos:valueA // NOT
                    {:
                        RESULT = "! " + valueA;
                        vValue = RESULT.toString();
                    :}
        | ABRECUENTO:lParen jugueteslogicos:valueA CIERRECUENTO:rParen // (expr)
                    {:
                        RESULT = lParen + " " + valueA + " " + rParen;
                        vValue = RESULT.toString();
                    :}
        | ABRECUENTO:lParen jugueteslogicos:valueA CIERRECUENTO:rParen jugueteslogicos // (expr) op expr
                    {:
                        RESULT = lParen + " " + valueA + " " + rParen;
                        vValue = RESULT.toString();
                    :}
        | PERSONA:valueA GASPAR L_FATHERCHRISTMAS:valueB // identificador
                    {:
                        RESULT = valueA + " # " + valueB;
                        vValue = RESULT.toString();
                    :}
        | L_FATHERCHRISTMAS:valueA // literal boolean
                    {:
                        RESULT = valueA;
                        vValue = RESULT.toString();
                    :};

// unarios (incremento, decremento, etc)
juguetesunarios ::= GRINCH juguetesaritmeticos // ++
                | QUIEN juguetesaritmeticos; // --

// asignacion <=
jugueteasignacion_entera ::= PERSONA:name ENTREGA piezasenteras:value
                                {:
                                    vName = name;
                                    vValue = value.toString();
                                :}
                            | PERSONA:name ENTREGA GRINCH piezasenteras:value
                                {:
                                    vName = name;
                                    vValue = value.toString();
                                :}
                            | PERSONA:name ENTREGA QUIEN piezasenteras:value
                                {:
                                    vName = name;
                                    vValue = value.toString();
                                :};

jugueteasignacion_flotante ::= PERSONA:name ENTREGA piezasflotantes:value
                                {:
                                    vName = name;
                                    vValue = value.toString();
                                :}
                            | PERSONA:name ENTREGA GRINCH piezasflotantes:value
                                {:
                                    vName = name;
                                    vValue = value.toString();
                                :}
                            | PERSONA:name ENTREGA QUIEN piezasflotantes:value
                                {:
                                    vName = name;
                                    vValue = value.toString();
                                :};


jugueteasignacion ::= LOCAL SANTACLAUS jugueteasignacion_entera // enteros
                        {:
                            listaTablasSimbolos.get(currentHash).add("variable int (" + vName + ") : " + vValue);
                        :}
        | LOCAL PERENOEL jugueteasignacion_flotante // flotantes
            {:
                listaTablasSimbolos.get(currentHash).add("variable float (" + vName + ") : " + vValue);
            :}
        |jugueteasignacion_entera
            {:
                listaTablasSimbolos.get(currentHash).add("variable int (" + vName + ") : " + vValue);
            :}
        |jugueteasignacion_flotante
            {:
                listaTablasSimbolos.get(currentHash).add("variable float (" + vName + ") : " + vValue);
            :}
        

        | LOCAL FATHERCHRISTMAS PERSONA:vName ENTREGA jugueteslogicos:vValue // booleanos
            {:
                listaTablasSimbolos.get(currentHash).add("variable boolean (" + vName + ") : " + vValue);
            :}
        | LOCAL KRISKRINGLE PERSONA:vName ENTREGA L_KRISKRINGLE:vValueCharmander // caracteres
            {:
                listaTablasSimbolos.get(currentHash).add("variable char (" + vName + ") : " + vValueCharmander.charAt(1));
            :}
        | LOCAL DEDMOROZ PERSONA:vName ENTREGA L_DEDMOROZ:vValue // strings ⚠️ valorar utilizar produccion de strings
            {:
                listaTablasSimbolos.get(currentHash).add("variable string (" + vName + ") : " + vValue);
            :}
        | LOCAL SANTACLAUS ABREEMPAQUE L_SANTACLAUS CIERREEMPAQUE PERSONA:vName ENTREGA ABREEMPAQUE piezaarregloentero:vValue CIERREEMPAQUE
            {:
                listaTablasSimbolos.get(currentHash).add("variable array int (" + vName + ") : [" + vValue + "]");
            :}
        | LOCAL KRISKRINGLE ABREEMPAQUE L_SANTACLAUS CIERREEMPAQUE PERSONA:vName ENTREGA ABREEMPAQUE piezaarreglochar:vValue CIERREEMPAQUE
            {:
                listaTablasSimbolos.get(currentHash).add("variable array char (" + vName + ") : [" + vValue + "]");
            :}

        | PERSONA:vName ENTREGA jugueteslogicos:vValue // booleanos
            {:
                listaTablasSimbolos.get(currentHash).add("variable boolean (" + vName + ") : " + vValue);
            :}
        | PERSONA:vName ENTREGA L_KRISKRINGLE:vValueCharmander // caracteres
            {:
                listaTablasSimbolos.get(currentHash).add("variable char (" + vName + ") : " + vValueCharmander.charAt(1));
            :}
        | PERSONA:vName ENTREGA L_DEDMOROZ:vValue // strings ⚠️ valorar utilizar produccion de strings
            {:
                listaTablasSimbolos.get(currentHash).add("variable string (" + vName + ") : " + vValue);
            :}
        | PERSONA:vName ENTREGA ABREEMPAQUE arreglosarreglados:vValue CIERREEMPAQUE
            {:
                listaTablasSimbolos.get(currentHash).add("variable array (" + vName + ") : [" + vValue + "]");
            :}
        ;

juguetesdefinidos ::= LOCAL SANTACLAUS PERSONA:idParam // int identificador
                {:
                //int
                    listaTablasSimbolos.get(currentHash).add("Variable int " + idParam + ": sin valor");
                :}   
            | LOCAL PERENOEL PERSONA:idParam // float identificador
                {:
                //float
                    listaTablasSimbolos.get(currentHash).add("Variable float " + idParam + ": sin valor");
                :}   
            | LOCAL FATHERCHRISTMAS PERSONA:idParam // boolean identificador
                {:
                //boolean
                    listaTablasSimbolos.get(currentHash).add("Variable boolean " + idParam + ": sin valor");
                :}   
            | LOCAL KRISKRINGLE PERSONA:idParam // char identificador
                {:
                //char
                    listaTablasSimbolos.get(currentHash).add("Variable char " + idParam + ": sin valor");
                :}   
            | LOCAL DEDMOROZ PERSONA:idParam // string identificador
                {:
                //string
                    listaTablasSimbolos.get(currentHash).add("Variable string " + idParam + ": sin valor");
                :}   
            | LOCAL PAPANOEL PERSONA:idParam // array identificador
                {:
                //array
                    listaTablasSimbolos.get(currentHash).add("Variable array " + idParam + ": sin valor");
                :};
                
arreglosarreglados ::= piezaarregloentero:value
                        {:
                            RESULT = value;
                            vValue = RESULT.toString();
                        :}
                    | piezaarreglochar:value
                    {:
                        RESULT = value;
                        vValue = RESULT.toString();
                    :}
                    ;
piezaarregloentero ::= piezasenteras:value
                        {:
                            RESULT = value;
                            vValue = RESULT.toString();
                        :}
                    | piezasenteras:valueA COMA:coma piezaarregloentero:valueB
                        {:
                            RESULT = valueA + " " + coma + " " + valueB;
                            vValue = RESULT.toString();
                        :}
                    ;

piezaarreglochar ::= L_KRISKRINGLE:value
                        {:
                            RESULT = value;
                            //vValueCharmander = RESULT.toString().charAt(0);
                            vValue = RESULT.toString();
                        :}
                    | L_KRISKRINGLE:valueA COMA:coma piezaarreglochar:valueB
                        {:
                            RESULT = valueA + " " + coma + " " + valueB;
                            //vValueCharmander = RESULT.toString().charAt(0);
                            vValue = RESULT.toString();
                        :}
                    | PERSONA:valueA COMA:coma piezaarreglochar:valueB
                        {:
                            RESULT = valueA + " " + coma + " " + valueB;
                            //vValueCharmander = RESULT.toString().charAt(0);
                            vValue = RESULT.toString();
                        :}
                        ;

// estructuras de control
juguetesdecontrol ::= ELFO ABRECUENTO controldejuguete CIERRECUENTO ABREREGALO juguetes CIERRAREGALO
                | ELFO ABRECUENTO controldejuguete CIERRECUENTO ABREREGALO juguetes CIERRAREGALO adicionalesdecontrol;

adicionalesdecontrol ::= HADA ABRECUENTO controldejuguete CIERRECUENTO ABREREGALO juguetes CIERRAREGALO 
                | HADA ABRECUENTO controldejuguete CIERRECUENTO ABREREGALO juguetes CIERRAREGALO adicionalesdecontrol
                | DUENDE ABREREGALO juguetes CIERRAREGALO;




iteradordejuguetes ::= ENVUELVE ABRECUENTO parametrosdeenvoltura CIERRECUENTO ABREREGALO juguetes CIERRAREGALO
                | HACE ABREREGALO juguetes CIERRAREGALO REVISA ABRECUENTO controldejuguete CIERRECUENTO FINREGALO;

parametrosdeenvoltura ::= LOCAL SANTACLAUS jugueteasignacion_entera FINREGALO controldejuguete FINREGALO juguetesunarios;

controldejuguete ::= juguetesrelacionales | jugueteslogicos;

// produccion de recuperacion de errores
error_production ::= error //{ System.out.println("Error: " + this.preceding().left + " - " + this.current_left()); }
                | error COMA error; //{ System.out.println("Error: cierre de llaves"); }
                
